import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
  ext {
    springBootVersion = "2.2.0.RELEASE"
  }

  repositories {
    mavenCentral()
  }
}

plugins {
  id 'java'
  id 'org.springframework.boot' version "$springBootVersion"
  id "org.flywaydb.flyway" version "5.2.4"
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
  mavenCentral()
}

dependencies {
  compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "$springBootVersion")
  compile(group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: "$springBootVersion")

  compile group: 'mysql', name: 'mysql-connector-java', version: '8.0.17'

  testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "$springBootVersion")
  testCompile(group: 'org.mockito', name: 'mockito-core', version: '2.23.4')
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
    "WELCOME_MESSAGE": "hello",
    "SPRING_DATASOURCE_URL": developmentDbUrl,
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&password=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
    "WELCOME_MESSAGE": "Hello from test",
    "SPRING_DATASOURCE_URL": testDbUrl,
])

flyway {
  url = developmentDbUrl
  user = "tracker"
  password = ""
  locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
  url = testDbUrl
  password = "tracker"
}
